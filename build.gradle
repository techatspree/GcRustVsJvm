plugins {


    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
}

group = 'de.akquinet'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

kotlin {
    jvmToolchain(21)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

tasks.register('computeAverageIncomeKotlin', JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    mainClass = "ComputeAverageIncomeKt"
}

// GraalVM must be installed and GRAALVM_HOME must be set
// https://www.graalvm.org
tasks.register('computeAverageIncomeGraalVM', JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    mainClass = "ComputeAverageIncomeKt"

    def graalvmHome = System.getenv('GRAALVM_HOME')
    if (graalvmHome) {
        executable = "${graalvmHome}/bin/java"
    } else {
        doFirst {
            throw new GradleException("GRAALVM_HOME environment variable is not set. Please set it to your GraalVM installation directory.")
        }
    }
}

// Stack must be installed and the executable must be in the path
// https://docs.haskellstack.org/en/stable/README/

tasks.register('cleanAverageIncomeHaskell', Exec) {
    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "clean"
}

tasks.register('buildForProfileAverageIncomeHaskell', Exec) {
    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "build", "--profile"
}
tasks.register('buildAverageIncomeHaskell', Exec) {
    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "build"
}

tasks.register('computeAverageIncomeHaskell', Exec) {
    dependsOn(cleanAverageIncomeHaskell)
    dependsOn(buildAverageIncomeHaskell)
    buildAverageIncomeHaskell.mustRunAfter(cleanAverageIncomeHaskell)

    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "exec", "compute-average-income-exe", "+RTS", "-N6"
}

tasks.register('profileAverageIncomeHaskell', Exec) {
    dependsOn(cleanAverageIncomeHaskell)
    dependsOn(buildForProfileAverageIncomeHaskell)
    buildForProfileAverageIncomeHaskell.mustRunAfter(cleanAverageIncomeHaskell)

    workingDir "./src/main/Haskell/compute-average-income"
    commandLine "stack", "exec", "--profile", "--", "compute-average-income-profile-exe", "+RTS", "-s"
    // simple statistics
    //commandLine "stack", "exec" , "--profile", "--", "compute-average-income-profile-exe", "+RTS" , "-P", "-S" // extensive profile
    //commandLine "stack", "exec" , "--profile", "--", "compute-average-income-exe", "+RTS" , "-P", "-s"
}


// Rust must be installed using rustup
// https://www.rust-lang.org
tasks.register('computeAverageIncomeRust', Exec) {
    workingDir "./src/main/Rust/compute_average_income"
    commandLine "cargo", "run", "--release", "--package", "compute_average_income", "--bin", "compute_average_income"
}

tasks.register('computeAverageIncome') {
    dependsOn "computeAverageIncomeRust"
    dependsOn "computeAverageIncomeHaskell"
    dependsOn "computeAverageIncomeKotlin"
    dependsOn "computeAverageIncomeGraalVM"
}
